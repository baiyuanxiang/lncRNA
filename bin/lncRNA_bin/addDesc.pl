#!/usr/bin/perl -w

=pod
description: add description
author: Zhang Fangxian, zhangfx@genomics.cn
created date: 20100715
modified date: 20100715
=cut

use strict;
use Getopt::Long;

my ($input, $desc, $output, $help);

GetOptions("input:s" => \$input, "desc:s" => \$desc, "output:s" => \$output, "help|?" => \$help);

if (!defined $input || !defined $desc || defined $help) {
	die << "USAGE";
description: add description
usage: perl $0 [options]
options:
	-input <file> *  input file
	-desc <file> *   description file generated by genDesc.pl
	-output <file>   output file, default is input file

	-help|?          help information
e.g.:
	perl $0 -input A.xls -desc human.desc -output A.xls
USAGE
}

$output ||= $input;

my ($header, %descs, $column);

&showLog("read file $desc");
open DESC, "< $desc" or die $!;

chomp($header = <DESC>);
$header =~ s/^[^\t]*\t//;
my @tabs = split /\t/, $header;
$column = @tabs;

while (<DESC>) {
	next if (/(^#)|(^\s*$)/);
	chomp;
	@tabs = split /\t/, $_, 2;
	$descs{$tabs[0]} = $tabs[1];
}
close DESC;

&showLog("read file $input");
open IN, "< $input" or die $!;
open OUT, "> $output.$$" or die $!;
chomp(my $temp = <IN>);
$temp =~ s/\s+$//;
print OUT "$temp\t$header\n";
while (my $line = <IN>) {
	chomp($line);
	$line =~ s/\s+$//;
	my $gene = (split /\t/, $line)[0];
	my $desc = exists $descs{$gene} ? "\t$descs{$gene}" : "\t-" x $column;
	print OUT "$line$desc\n";
}
close OUT;
close IN;

rename "$output.$$", $output or die $!;

&showLog("done");

exit 0;

sub showLog {
	my ($info) = @_;
	my @times = localtime; # sec, min, hour, day, month, year
	print STDERR sprintf("[%d-%02d-%02d %02d:%02d:%02d] %s\n", $times[5] + 1900, $times[4] + 1, $times[3], $times[2], $times[1], $times[0], $info);
}
